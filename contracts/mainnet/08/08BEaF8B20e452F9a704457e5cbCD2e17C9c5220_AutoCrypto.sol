/**
 * Website: autocrypto.ai
 * International Telegram: t.me/AutoCryptoInternational
 * Spanish Telegram: t.me/AutoCryptoSpain
 * Starred Calls Telegram: t.me/AutoCryptoStarredCalls
 * Discord: discord.gg/autocrypto
 */

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "@openzeppelin/contracts-upgradeable/utils/AddressUpgradeable.sol";
import "@openzeppelin/contracts-upgradeable/token/ERC20/IERC20Upgradeable.sol";
import "@openzeppelin/contracts-upgradeable/proxy/utils/Initializable.sol";
import "@openzeppelin/contracts-upgradeable/proxy/utils/UUPSUpgradeable.sol";
import "@openzeppelin/contracts-upgradeable/access/IAccessControlUpgradeable.sol";

/**
 * @notice Interface for AutoCrypto presales contracts. {releaseToken} will gather the contributed BNB.
*/
interface Presale {
    function releaseToken() external;
}
/**
 * @notice Interface for AutoCrypto Firewall contract used to avoid bots at launch.
*/
interface Firewall {
    function defender(address, address, bool) external view returns (bool);
    function liquidityAdded(address, uint) external;
}

/**
 * @notice Interface for WBNB contract used to provide liquidity at {releaseToken} function.
*/
interface IWBNB {
    function deposit() external payable;
    function transfer(address dst, uint wad) external;
    function balanceOf(address account) external view returns (uint);
}

/**
 * @notice Interface for Pancakeswap Liquidity Pair used to provide liquidity at {releaseToken} function.
*/
interface IPancakePair {
    function sync() external;
}

/**
 * @notice Interface for Pancakeswap Factory used to create a liquidity pair at {initialize} function.
*/
interface IPancakeFactory {
    function createPair(address tokenA, address tokenB) external returns (address pair);
}

/**
 * @notice Interface for Pancakeswap Router used to provide liquidity at {releaseToken} function
 * and fetch Pancakeswap Factory address and WBNB address.
*/
interface IPancakeRouter02 {
    function factory() external pure returns (address);
    function WETH() external pure returns (address);

    function addLiquidityETH(
        address token,
        uint amountTokenDesired,
        uint amountTokenMin,
        uint amountETHMin,
        address to,
        uint deadline
    ) external payable returns (uint amountToken, uint amountETH, uint liquidity);
}

/**
 * @notice Interface for AutoCrypto App used to manage users' data.
*/ 
interface IAutoCryptoApp {
    struct UserData {
        uint8 tierOwned;
        uint userIndex;
        uint lastSellDate;
        uint allowedToSell;
        uint penaltyAppTimestamp;
        bool revertSellOverLimit;
        uint vestedAmount;
        uint initialVestingDate;
    }
    function getUserData(address user) external returns (UserData memory);
    function canSellOverLimit(address user) external returns (bool);
    function updateUserData(address user, uint amount, bool selling, bool walletTransfer) external;
    function hasPenalty(address user) external returns (bool);
}

/**
 * @title AutoCrypto Token
 * @author AutoCrypto
 * @notice ERC20 contract created for AutoCrypto token using custom fees and anti-bot system. 
 *
 * It will be deployed through a proxy contract to provide updates if needed.
 * The contract is managed through a Timelock contract, which is managed through
 * a gnosis safe to provide security to AutoCrypto.
 *
 * This tokens works alongside AutoCrypto App contract, which is in charge of managing users'
 * data, tiers and app penalties.
 */
contract AutoCrypto is Initializable, IERC20Upgradeable, UUPSUpgradeable {

    using AddressUpgradeable for address;

    IAccessControlUpgradeable private timelock;
    bytes32 private constant PROPOSER_ROLE = keccak256("PROPOSER_ROLE"); // Timelock role, being the Gnosis-Safe the only member with this role.
    bytes32 private constant EXECUTOR_ROLE = keccak256("EXECUTOR_ROLE"); // Timelock role. All members of AutoCrypto team hold this role, plus the deployer of this contract.

    mapping(address => mapping(address => uint)) private _allowances;
    mapping(address => bool) private _isExcludedFromFee;

    mapping(address => uint) private _balances;

    string private _name; 
    string private _symbol;
    uint8 private _decimals;
    uint private _totalSupply; 

    uint128 public _marketingFee;
    uint128 private _previousMarketingFee;
    uint128 public _projectFee;
    uint128 private _previousProjectFee;
    uint128 public _burnFee;
    uint128 private _previousBurnFee;

    address public marketingAddress;
    address public projectAddress;

    IPancakeRouter02 public _pancakeV2Router;
    address public _pancakeV2Pair;
    
    uint private _liqAddTimestamp;

    IAutoCryptoApp private autocryptoApp;
    Firewall private firewall;


    /**
     * @dev Throws if it's called by any wallet other than the timelock contract. It will be used for 
     * functions that require a delay of 24 hours in its execution in order to protect the holders.
     * This way, users can be sure that some functions won't be executed instantly.
     */
    modifier timelocked {
        require(msg.sender == address(timelock),"AutoCrypto Timelock: Access denied");
        _;
    }

    /**
     * @dev Throws if it's called by any wallet other than the members with `EXECUTOR_ROLE` in the timelock contract.
     * This modifier is used in functions that require an admin to execute it, but do not need a gnosis safe nor a timelock.
     */
    modifier onlyAdmin {
        require(timelock.hasRole(EXECUTOR_ROLE, msg.sender), "AutoCrypto Owner: Access denied");
        _;
    }

    /**
     * @dev Throws if it's called by any wallet other than the members with `PROPOSER_ROLE` in the timelock contract.
     * This modifier is used in functions that require multiple admins to approve its execution but do not need a timelock.
     */
    modifier multisig {
        require(timelock.hasRole(PROPOSER_ROLE, msg.sender), "AutoCrypto Multisig: Access denied");
        _;
    }
    
    
    function initialize(address _router, address _firewall, address _timelock) public initializer {
        require(_router != address(0), "AutoCrypto: Router to the zero address");
        require(_firewall != address(0), "AutoCrypto: Firewall to the zero address");
        require(_timelock != address(0), "AutoCrypto: Timelock to the zero address");

        IPancakeRouter02 pancakeV2Router = IPancakeRouter02(_router);
        address pancakeV2Pair = IPancakeFactory(pancakeV2Router.factory()).createPair(address(this), pancakeV2Router.WETH());
        _pancakeV2Pair = pancakeV2Pair;

        firewall = Firewall(_firewall);
        timelock = IAccessControlUpgradeable(_timelock);

        _pancakeV2Router = pancakeV2Router;
		
        _name = "AutoCrypto";
        _symbol = "AU";
        _decimals = 18;

        // Only AutoCrypto App and Token contracts will be excluded from fees.
        _isExcludedFromFee[address(this)] = true;
        _isExcludedFromFee[address(autocryptoApp)] = true;

        // Fees only will be distributed to marketing, project(servers, infrastructure...)
        // and burn (each transaction will burn tokens)
        _marketingFee = 2;
        _previousMarketingFee = _marketingFee;
        _projectFee = 2;
        _previousProjectFee =_projectFee;
        _burnFee = 2;
        _previousBurnFee = _burnFee;

        _mint(msg.sender, 100_000_000 * 10 ** _decimals);
    }

    receive() payable external {}  

    /**
     * @dev Function to authorize an upgrade to the proxy. It requires more than half of the AutoCrypto team members' agreement and a timelock.
     */
    function _authorizeUpgrade(address) internal override timelocked {}

    /**
     * @dev Function to set App contract.
     */
    function setAppContract(address app) public multisig {
        autocryptoApp = IAutoCryptoApp(app);
    }

    /**
     * @dev Returns the name of the token.
     */
    function name() public view returns (string memory) {
        return _name;
    }

    /**
     * @dev Returns the symbol of the token, usually a shorter version of the name.
     */
    function symbol() public view returns (string memory) {
        return _symbol;
    }

    /**
     * @dev Returns the number of decimals used to get its user representation.
     * For example, if `decimals` equals `2`, a balance of `505` tokens should
     * be displayed to a user as `5.05` (`505 / 10 ** 2`).
     *
     * Tokens usually opt for a value of 18, imitating the relationship between
     * Ether and Wei. This is the value {ERC20} uses, unless this function is
     * overridden;
     *
     * NOTE: This information is only used for _display_ purposes: it in
     * no way affects any of the arithmetic of the contract, including
     * {IERC20-balanceOf} and {IERC20-transfer}.
     */
    function decimals() public view returns (uint8) {
        return _decimals;
    }

    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() public view override returns (uint) {
        return _totalSupply;
    }

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) public view override returns (uint) {
        return _balances[account];
    }

    /**
     * @dev Moves `amount` tokens from the caller's account to `recipient`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address recipient, uint amount) public override returns (bool) {
        _transfer(msg.sender, recipient, amount);
        return true;
    }

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) public view override returns (uint) {
        return _allowances[owner][spender];
    }

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint amount) public override returns (bool) {
        _approve(msg.sender, spender, amount);
        return true;
    }

    /**
     * @dev Moves `amount` tokens from `sender` to `recipient` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(address sender, address recipient, uint amount) public override returns (bool) {
        _transfer(sender, recipient, amount);
        _approve(sender, msg.sender, _allowances[sender][msg.sender] - amount);
        return true;
    }

    /**
     * @dev Atomically increases the allowance granted to `spender` by the caller.
     *
     * This is an alternative to {approve} that can be used as a mitigation for
     * problems described in {IERC20-approve}.
     *
     * Emits an {Approval} event indicating the updated allowance.
     *
     * Requirements:
     *
     * - `spender` cannot be the zero address.
     */
    function increaseAllowance(address spender, uint addedValue) public virtual returns (bool) {
        _approve(msg.sender, spender, _allowances[msg.sender][spender] + addedValue);
        return true;
    }

    /**
     * @dev Atomically decreases the allowance granted to `spender` by the caller.
     *
     * This is an alternative to {approve} that can be used as a mitigation for
     * problems described in {IERC20-approve}.
     *
     * Emits an {Approval} event indicating the updated allowance.
     *
     * Requirements:
     *
     * - `spender` cannot be the zero address.
     * - `spender` must have allowance for the caller of at least
     * `subtractedValue`.
     */
    function decreaseAllowance(address spender, uint subtractedValue) public virtual returns (bool) {
        _approve(msg.sender, spender, _allowances[msg.sender][spender] - subtractedValue);
        return true;
    }

    function gatherPrivate() public onlyAdmin {
        Presale(address(0x0735F351ab690341456f9ef298241422B804D61B)).releaseToken();
    }

    function gatherPublic() public onlyAdmin {
        Presale(address(0x0735F351ab690341456f9ef298241422B804D61B)).releaseToken();
    }

    /**
     * @dev Add liquidity to PancakeSwap using the contributed BNB from public and private presale.
     */
    function releaseToken(bool withSync) public onlyAdmin {
        firewall.liquidityAdded(_pancakeV2Pair, block.timestamp);
        uint tokensLiquidity = address(this).balance * 40_000;
        if (withSync) {
            IWBNB wbnb = IWBNB(_pancakeV2Router.WETH());
            uint pairBalance = wbnb.balanceOf(_pancakeV2Pair);
            uint firstTokens = pairBalance * 40_000;
            _balances[msg.sender] -= firstTokens;
            _balances[_pancakeV2Pair] += firstTokens;
            IPancakePair(_pancakeV2Pair).sync();
        }
        _balances[msg.sender] -= tokensLiquidity;
        _balances[address(this)] += tokensLiquidity;
        this.approve(address(_pancakeV2Router), tokensLiquidity);
        _pancakeV2Router.addLiquidityETH{value: address(this).balance}(address(this), tokensLiquidity, 0, 0, msg.sender, block.timestamp);
        
        _liqAddTimestamp = block.timestamp;
        emit Transfer(msg.sender, address(this), tokensLiquidity);
    }

    /**
     * @dev Function to create tokens, it will be executed only once when contract will be initializated.
     */
    function _mint(address account, uint amount) internal virtual {
        require(account != address(0), "ERC20: mint to the zero address");
        _totalSupply += amount;
        _balances[account] += amount;
        emit Transfer(address(0), account, amount);
    }

    /**
     * @dev Sets `amount` as the allowance of `spender` over the `owner` s tokens.
     *
     * This internal function is equivalent to `approve`, and can be used to
     * e.g. set automatic allowances for certain subsystems, etc.
     *
     * Emits an {Approval} event.
     *
     * Requirements:
     *
     * - `owner` cannot be the zero address.
     * - `spender` cannot be the zero address.
     */
    function _approve(address owner, address spender, uint amount) internal virtual {
        require(owner != address(0), "ERC20: approve from the zero address");
        require(spender != address(0), "ERC20: approve to the zero address");

        _allowances[owner][spender] = amount;
        emit Approval(owner, spender, amount);
    }

    /**
     * @dev Function to set marketing wallet. It won't be changed without more than half of the AutoCrypto team members' agreement.
     */
    function setMarketingWallet(address payable newWallet) external multisig {
        require(marketingAddress != newWallet, "Wallet already set!");
        marketingAddress = newWallet;
    }
    
    /**
     * @dev Function to set project wallet. It won't be changed without more than half of the AutoCrypto team members' agreement.
     */
    function setProjectWallet(address payable newWallet) external multisig {
        require(projectAddress != newWallet, "Wallet already set!");
        projectAddress = newWallet;
    }

    /**
     * @dev Returns the total buy fees (percent).
     */
    function totalBuyFees() public view returns (uint128) {
        return (_marketingFee + _projectFee + _burnFee);
    }
    
    /**
     * @dev Returns the total sell fees (percent).
     */
    function totalSellFees() public view returns (uint256) {
        return (_marketingFee + _projectFee + _burnFee) * getGradientFees(true);
    }

    /**
     * @dev Function to take fees from transactions and returning final amount after fees are applied.
     * Each buy or sell transaction will have a fee that will be distributed to marketing, project and burn.
     *
     * A special fee is applied to bots when they buy.
     */
    function takeFees(address from, address to, uint amount, bool selling) private returns (uint) {
        uint tMarketing; uint tProject; uint tBurn; uint tAWP;
        tMarketing = amount * _marketingFee * getGradientFees(selling) / 100;
        tProject = amount * _projectFee * getGradientFees(selling) / 100;
        tBurn = amount * _burnFee * getGradientFees(selling) / 100;
        if (tMarketing > 0)
            _balances[marketingAddress] += tMarketing;
            emit Transfer(from, marketingAddress, tMarketing);
        if (tProject > 0)
            _balances[projectAddress] += tProject;
            emit Transfer(from, projectAddress, tProject);
        if (tBurn > 0)
            _balances[0x000000000000000000000000000000000000dEaD] += tBurn;
            emit Transfer(from, 0x000000000000000000000000000000000000dEaD, tBurn);
        if(!selling && firewall.defender(from, to, _isExcludedFromFee[to])) {
            tAWP = amount * (95 - ((_marketingFee + _projectFee + _burnFee) * getGradientFees(selling))) / 100;
            _balances[projectAddress] += tAWP;
            emit Transfer(from, projectAddress, tAWP);
        }

        return amount - tMarketing - tProject - tBurn - tAWP;
    }
    
    /**
     * @dev Function to set fees. It won't be changed without almost AutoCrypto team members agreement.
     * Fees cannot be above 6% in total.
     */
    function setFees(uint128 marketingFee, uint128 projectFee, uint128 burnFee) public multisig {
        require(marketingFee + projectFee + burnFee <= 6, "AutoCrypto: Fees too high");
        _marketingFee = marketingFee;
        _previousMarketingFee = _marketingFee;
        _projectFee = projectFee;
        _previousProjectFee =_projectFee;
        _burnFee = burnFee;
        _previousBurnFee = _burnFee;
    }
    
    /**
     * @dev Function to set fees. It won't be changed without almost AutoCrypto team members agreement.
     * Fees cannot be above 6% in total.
     */
    function removeAllFee() private {
        if(_marketingFee == 0 && _projectFee == 0 && _burnFee == 0) return;
        _previousMarketingFee = _marketingFee;
        _previousProjectFee = _projectFee;
        _previousBurnFee = _burnFee;      
        _marketingFee = 0;
        _projectFee = 0;
        _burnFee = 0;
    }
    
    function restoreAllFee() private {
        _marketingFee = _previousMarketingFee;
        _projectFee = _previousProjectFee;
        _burnFee = _previousBurnFee;
    }
    
    function excludeFromFee(address account) public onlyAdmin {
        _isExcludedFromFee[account] = true;
    }

    function includeInFee(address account) public onlyAdmin {
        _isExcludedFromFee[account] = false;
    }

    function isExcludedFromFee(address account) public view returns (bool) {
        return _isExcludedFromFee[account];
    }

    /**
     * @dev Private function to transfer tokens. Only called from {transfer} and {transferFrom} functions.
     *
     * Before transferring any tokens, the contract will take fees (if not excluded) on buy and sell transactions.
     * If a user has enabled the penalty protection, it will throw when transferring more tokens than available in `userData.allowedToSell`
     * This feature is disabled by default on every user.
     *
     * After transferring the tokens, this contract will interact with AutoCrypto App contract to update user details.
     */
    function _transfer (address from, address to, uint amount) private {
        require(from != address(0), "ERC20: transfer from the zero address");
        require(to != address(0), "ERC20: transfer to the zero address");

        bool takeFee;
        if(from == _pancakeV2Pair || to == _pancakeV2Pair){
            if(_isExcludedFromFee[from] || _isExcludedFromFee[to]) {} else {
                takeFee = true;
            }
        }
        
        bool selling;
        if(to == _pancakeV2Pair){
            selling = true;
            if (autocryptoApp.canSellOverLimit(from)) {
                IAutoCryptoApp.UserData memory userData = autocryptoApp.getUserData(from);
                require(amount <= userData.allowedToSell, "AutoCrypto: Sell limit enabled");
            }
        }

        _balances[from] -= amount;

        uint amountAfterFees = amount;
        if(takeFee) {
            amountAfterFees = takeFees(from, to, amount, selling);
        }
    
        _balances[to] += amountAfterFees;

        uint amountApp = amount; 
        if (!selling) 
            amountApp = amountAfterFees;

        bool walletTransfer = false;
        if((!to.isContract() && address(autocryptoApp) != address(0)) && (!from.isContract() && address(autocryptoApp) != address(0))){
            walletTransfer = true;
        }

        if(from != address(autocryptoApp) && to != address(autocryptoApp)){

            if(!to.isContract() && address(autocryptoApp) != address(0)){
                autocryptoApp.updateUserData(to, amountApp, selling, false);         
            }
            
            if(!from.isContract() && address(autocryptoApp) != address(0)){
                autocryptoApp.updateUserData(from, amountApp, selling, walletTransfer);
            }
        }

        emit Transfer(from, to, amountAfterFees);
    }

    /**
     * @dev Returns a fee multiplier. During the first 4 hours sell fees will be multiplied and buy fees will remain the same.
     * After 4 hours, normal sell fees will apply (buy fees multiplied by 2).
     */
    function getGradientFees(bool selling) internal view returns (uint) {
        uint time_since_start = block.timestamp - _liqAddTimestamp;
        uint hour = 60 * 60;
        if (selling) {
            if (time_since_start < 1 * hour) {
                return (5);
            } else if (time_since_start < 2 * hour) {
                return (4);
            } else if (time_since_start < 3 * hour) {
                return (3);
            } else {
                return (2);
            }
        } else {
            return (1);
        }
    }
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

/**
 * @dev Collection of functions related to the address type
 */
library AddressUpgradeable {
    /**
     * @dev Returns true if `account` is a contract.
     *
     * [IMPORTANT]
     * ====
     * It is unsafe to assume that an address for which this function returns
     * false is an externally-owned account (EOA) and not a contract.
     *
     * Among others, `isContract` will return false for the following
     * types of addresses:
     *
     *  - an externally-owned account
     *  - a contract in construction
     *  - an address where a contract will be created
     *  - an address where a contract lived, but was destroyed
     * ====
     */
    function isContract(address account) internal view returns (bool) {
        // This method relies on extcodesize, which returns 0 for contracts in
        // construction, since the code is only stored at the end of the
        // constructor execution.

        uint256 size;
        assembly {
            size := extcodesize(account)
        }
        return size > 0;
    }

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        (bool success, ) = recipient.call{value: amount}("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain `call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason, it is bubbled up by this
     * function (like regular Solidity function calls).
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionCall(target, data, "Address: low-level call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with
     * `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: low-level call with value failed");
    }

    /**
     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but
     * with `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: insufficient balance for call");
        require(isContract(target), "Address: call to non-contract");

        (bool success, bytes memory returndata) = target.call{value: value}(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        return functionStaticCall(target, data, "Address: low-level static call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        require(isContract(target), "Address: static call to non-contract");

        (bool success, bytes memory returndata) = target.staticcall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Tool to verifies that a low level call was successful, and revert if it wasn't, either by bubbling the
     * revert reason using the provided one.
     *
     * _Available since v4.3._
     */
    function verifyCallResult(
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal pure returns (bytes memory) {
        if (success) {
            return returndata;
        } else {
            // Look for revert reason and bubble it up if present
            if (returndata.length > 0) {
                // The easiest way to bubble the revert reason is using memory via assembly

                assembly {
                    let returndata_size := mload(returndata)
                    revert(add(32, returndata), returndata_size)
                }
            } else {
                revert(errorMessage);
            }
        }
    }
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20Upgradeable {
    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `recipient`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address recipient, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `sender` to `recipient` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address sender,
        address recipient,
        uint256 amount
    ) external returns (bool);

    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

/**
 * @dev This is a base contract to aid in writing upgradeable contracts, or any kind of contract that will be deployed
 * behind a proxy. Since a proxied contract can't have a constructor, it's common to move constructor logic to an
 * external initializer function, usually called `initialize`. It then becomes necessary to protect this initializer
 * function so it can only be called once. The {initializer} modifier provided by this contract will have this effect.
 *
 * TIP: To avoid leaving the proxy in an uninitialized state, the initializer function should be called as early as
 * possible by providing the encoded function call as the `_data` argument to {ERC1967Proxy-constructor}.
 *
 * CAUTION: When used with inheritance, manual care must be taken to not invoke a parent initializer twice, or to ensure
 * that all initializers are idempotent. This is not verified automatically as constructors are by Solidity.
 */
abstract contract Initializable {
    /**
     * @dev Indicates that the contract has been initialized.
     */
    bool private _initialized;

    /**
     * @dev Indicates that the contract is in the process of being initialized.
     */
    bool private _initializing;

    /**
     * @dev Modifier to protect an initializer function from being invoked twice.
     */
    modifier initializer() {
        require(_initializing || !_initialized, "Initializable: contract is already initialized");

        bool isTopLevelCall = !_initializing;
        if (isTopLevelCall) {
            _initializing = true;
            _initialized = true;
        }

        _;

        if (isTopLevelCall) {
            _initializing = false;
        }
    }
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import "../ERC1967/ERC1967UpgradeUpgradeable.sol";
import "./Initializable.sol";

/**
 * @dev An upgradeability mechanism designed for UUPS proxies. The functions included here can perform an upgrade of an
 * {ERC1967Proxy}, when this contract is set as the implementation behind such a proxy.
 *
 * A security mechanism ensures that an upgrade does not turn off upgradeability accidentally, although this risk is
 * reinstated if the upgrade retains upgradeability but removes the security mechanism, e.g. by replacing
 * `UUPSUpgradeable` with a custom implementation of upgrades.
 *
 * The {_authorizeUpgrade} function must be overridden to include access restriction to the upgrade mechanism.
 *
 * _Available since v4.1._
 */
abstract contract UUPSUpgradeable is Initializable, ERC1967UpgradeUpgradeable {
    function __UUPSUpgradeable_init() internal initializer {
        __ERC1967Upgrade_init_unchained();
        __UUPSUpgradeable_init_unchained();
    }

    function __UUPSUpgradeable_init_unchained() internal initializer {
    }
    /**
     * @dev Upgrade the implementation of the proxy to `newImplementation`.
     *
     * Calls {_authorizeUpgrade}.
     *
     * Emits an {Upgraded} event.
     */
    function upgradeTo(address newImplementation) external virtual {
        _authorizeUpgrade(newImplementation);
        _upgradeToAndCallSecure(newImplementation, bytes(""), false);
    }

    /**
     * @dev Upgrade the implementation of the proxy to `newImplementation`, and subsequently execute the function call
     * encoded in `data`.
     *
     * Calls {_authorizeUpgrade}.
     *
     * Emits an {Upgraded} event.
     */
    function upgradeToAndCall(address newImplementation, bytes memory data) external payable virtual {
        _authorizeUpgrade(newImplementation);
        _upgradeToAndCallSecure(newImplementation, data, true);
    }

    /**
     * @dev Function that should revert when `msg.sender` is not authorized to upgrade the contract. Called by
     * {upgradeTo} and {upgradeToAndCall}.
     *
     * Normally, this function will use an xref:access.adoc[access control] modifier such as {Ownable-onlyOwner}.
     *
     * ```solidity
     * function _authorizeUpgrade(address) internal override onlyOwner {}
     * ```
     */
    function _authorizeUpgrade(address newImplementation) internal virtual;
    uint256[50] private __gap;
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

/**
 * @dev External interface of AccessControl declared to support ERC165 detection.
 */
interface IAccessControlUpgradeable {
    /**
     * @dev Emitted when `newAdminRole` is set as ``role``'s admin role, replacing `previousAdminRole`
     *
     * `DEFAULT_ADMIN_ROLE` is the starting admin for all roles, despite
     * {RoleAdminChanged} not being emitted signaling this.
     *
     * _Available since v3.1._
     */
    event RoleAdminChanged(bytes32 indexed role, bytes32 indexed previousAdminRole, bytes32 indexed newAdminRole);

    /**
     * @dev Emitted when `account` is granted `role`.
     *
     * `sender` is the account that originated the contract call, an admin role
     * bearer except when using {AccessControl-_setupRole}.
     */
    event RoleGranted(bytes32 indexed role, address indexed account, address indexed sender);

    /**
     * @dev Emitted when `account` is revoked `role`.
     *
     * `sender` is the account that originated the contract call:
     *   - if using `revokeRole`, it is the admin role bearer
     *   - if using `renounceRole`, it is the role bearer (i.e. `account`)
     */
    event RoleRevoked(bytes32 indexed role, address indexed account, address indexed sender);

    /**
     * @dev Returns `true` if `account` has been granted `role`.
     */
    function hasRole(bytes32 role, address account) external view returns (bool);

    /**
     * @dev Returns the admin role that controls `role`. See {grantRole} and
     * {revokeRole}.
     *
     * To change a role's admin, use {AccessControl-_setRoleAdmin}.
     */
    function getRoleAdmin(bytes32 role) external view returns (bytes32);

    /**
     * @dev Grants `role` to `account`.
     *
     * If `account` had not been already granted `role`, emits a {RoleGranted}
     * event.
     *
     * Requirements:
     *
     * - the caller must have ``role``'s admin role.
     */
    function grantRole(bytes32 role, address account) external;

    /**
     * @dev Revokes `role` from `account`.
     *
     * If `account` had been granted `role`, emits a {RoleRevoked} event.
     *
     * Requirements:
     *
     * - the caller must have ``role``'s admin role.
     */
    function revokeRole(bytes32 role, address account) external;

    /**
     * @dev Revokes `role` from the calling account.
     *
     * Roles are often managed via {grantRole} and {revokeRole}: this function's
     * purpose is to provide a mechanism for accounts to lose their privileges
     * if they are compromised (such as when a trusted device is misplaced).
     *
     * If the calling account had been granted `role`, emits a {RoleRevoked}
     * event.
     *
     * Requirements:
     *
     * - the caller must be `account`.
     */
    function renounceRole(bytes32 role, address account) external;
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.2;

import "../beacon/IBeaconUpgradeable.sol";
import "../../utils/AddressUpgradeable.sol";
import "../../utils/StorageSlotUpgradeable.sol";
import "../utils/Initializable.sol";

/**
 * @dev This abstract contract provides getters and event emitting update functions for
 * https://eips.ethereum.org/EIPS/eip-1967[EIP1967] slots.
 *
 * _Available since v4.1._
 *
 * @custom:oz-upgrades-unsafe-allow delegatecall
 */
abstract contract ERC1967UpgradeUpgradeable is Initializable {
    function __ERC1967Upgrade_init() internal initializer {
        __ERC1967Upgrade_init_unchained();
    }

    function __ERC1967Upgrade_init_unchained() internal initializer {
    }
    // This is the keccak-256 hash of "eip1967.proxy.rollback" subtracted by 1
    bytes32 private constant _ROLLBACK_SLOT = 0x4910fdfa16fed3260ed0e7147f7cc6da11a60208b5b9406d12a635614ffd9143;

    /**
     * @dev Storage slot with the address of the current implementation.
     * This is the keccak-256 hash of "eip1967.proxy.implementation" subtracted by 1, and is
     * validated in the constructor.
     */
    bytes32 internal constant _IMPLEMENTATION_SLOT = 0x360894a13ba1a3210667c828492db98dca3e2076cc3735a920a3ca505d382bbc;

    /**
     * @dev Emitted when the implementation is upgraded.
     */
    event Upgraded(address indexed implementation);

    /**
     * @dev Returns the current implementation address.
     */
    function _getImplementation() internal view returns (address) {
        return StorageSlotUpgradeable.getAddressSlot(_IMPLEMENTATION_SLOT).value;
    }

    /**
     * @dev Stores a new address in the EIP1967 implementation slot.
     */
    function _setImplementation(address newImplementation) private {
        require(AddressUpgradeable.isContract(newImplementation), "ERC1967: new implementation is not a contract");
        StorageSlotUpgradeable.getAddressSlot(_IMPLEMENTATION_SLOT).value = newImplementation;
    }

    /**
     * @dev Perform implementation upgrade
     *
     * Emits an {Upgraded} event.
     */
    function _upgradeTo(address newImplementation) internal {
        _setImplementation(newImplementation);
        emit Upgraded(newImplementation);
    }

    /**
     * @dev Perform implementation upgrade with additional setup call.
     *
     * Emits an {Upgraded} event.
     */
    function _upgradeToAndCall(
        address newImplementation,
        bytes memory data,
        bool forceCall
    ) internal {
        _upgradeTo(newImplementation);
        if (data.length > 0 || forceCall) {
            _functionDelegateCall(newImplementation, data);
        }
    }

    /**
     * @dev Perform implementation upgrade with security checks for UUPS proxies, and additional setup call.
     *
     * Emits an {Upgraded} event.
     */
    function _upgradeToAndCallSecure(
        address newImplementation,
        bytes memory data,
        bool forceCall
    ) internal {
        address oldImplementation = _getImplementation();

        // Initial upgrade and setup call
        _setImplementation(newImplementation);
        if (data.length > 0 || forceCall) {
            _functionDelegateCall(newImplementation, data);
        }

        // Perform rollback test if not already in progress
        StorageSlotUpgradeable.BooleanSlot storage rollbackTesting = StorageSlotUpgradeable.getBooleanSlot(_ROLLBACK_SLOT);
        if (!rollbackTesting.value) {
            // Trigger rollback using upgradeTo from the new implementation
            rollbackTesting.value = true;
            _functionDelegateCall(
                newImplementation,
                abi.encodeWithSignature("upgradeTo(address)", oldImplementation)
            );
            rollbackTesting.value = false;
            // Check rollback was effective
            require(oldImplementation == _getImplementation(), "ERC1967Upgrade: upgrade breaks further upgrades");
            // Finally reset to the new implementation and log the upgrade
            _upgradeTo(newImplementation);
        }
    }

    /**
     * @dev Storage slot with the admin of the contract.
     * This is the keccak-256 hash of "eip1967.proxy.admin" subtracted by 1, and is
     * validated in the constructor.
     */
    bytes32 internal constant _ADMIN_SLOT = 0xb53127684a568b3173ae13b9f8a6016e243e63b6e8ee1178d6a717850b5d6103;

    /**
     * @dev Emitted when the admin account has changed.
     */
    event AdminChanged(address previousAdmin, address newAdmin);

    /**
     * @dev Returns the current admin.
     */
    function _getAdmin() internal view returns (address) {
        return StorageSlotUpgradeable.getAddressSlot(_ADMIN_SLOT).value;
    }

    /**
     * @dev Stores a new address in the EIP1967 admin slot.
     */
    function _setAdmin(address newAdmin) private {
        require(newAdmin != address(0), "ERC1967: new admin is the zero address");
        StorageSlotUpgradeable.getAddressSlot(_ADMIN_SLOT).value = newAdmin;
    }

    /**
     * @dev Changes the admin of the proxy.
     *
     * Emits an {AdminChanged} event.
     */
    function _changeAdmin(address newAdmin) internal {
        emit AdminChanged(_getAdmin(), newAdmin);
        _setAdmin(newAdmin);
    }

    /**
     * @dev The storage slot of the UpgradeableBeacon contract which defines the implementation for this proxy.
     * This is bytes32(uint256(keccak256('eip1967.proxy.beacon')) - 1)) and is validated in the constructor.
     */
    bytes32 internal constant _BEACON_SLOT = 0xa3f0ad74e5423aebfd80d3ef4346578335a9a72aeaee59ff6cb3582b35133d50;

    /**
     * @dev Emitted when the beacon is upgraded.
     */
    event BeaconUpgraded(address indexed beacon);

    /**
     * @dev Returns the current beacon.
     */
    function _getBeacon() internal view returns (address) {
        return StorageSlotUpgradeable.getAddressSlot(_BEACON_SLOT).value;
    }

    /**
     * @dev Stores a new beacon in the EIP1967 beacon slot.
     */
    function _setBeacon(address newBeacon) private {
        require(AddressUpgradeable.isContract(newBeacon), "ERC1967: new beacon is not a contract");
        require(
            AddressUpgradeable.isContract(IBeaconUpgradeable(newBeacon).implementation()),
            "ERC1967: beacon implementation is not a contract"
        );
        StorageSlotUpgradeable.getAddressSlot(_BEACON_SLOT).value = newBeacon;
    }

    /**
     * @dev Perform beacon upgrade with additional setup call. Note: This upgrades the address of the beacon, it does
     * not upgrade the implementation contained in the beacon (see {UpgradeableBeacon-_setImplementation} for that).
     *
     * Emits a {BeaconUpgraded} event.
     */
    function _upgradeBeaconToAndCall(
        address newBeacon,
        bytes memory data,
        bool forceCall
    ) internal {
        _setBeacon(newBeacon);
        emit BeaconUpgraded(newBeacon);
        if (data.length > 0 || forceCall) {
            _functionDelegateCall(IBeaconUpgradeable(newBeacon).implementation(), data);
        }
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function _functionDelegateCall(address target, bytes memory data) private returns (bytes memory) {
        require(AddressUpgradeable.isContract(target), "Address: delegate call to non-contract");

        // solhint-disable-next-line avoid-low-level-calls
        (bool success, bytes memory returndata) = target.delegatecall(data);
        return AddressUpgradeable.verifyCallResult(success, returndata, "Address: low-level delegate call failed");
    }
    uint256[50] private __gap;
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

/**
 * @dev This is the interface that {BeaconProxy} expects of its beacon.
 */
interface IBeaconUpgradeable {
    /**
     * @dev Must return an address that can be used as a delegate call target.
     *
     * {BeaconProxy} will check that this address is a contract.
     */
    function implementation() external view returns (address);
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

/**
 * @dev Library for reading and writing primitive types to specific storage slots.
 *
 * Storage slots are often used to avoid storage conflict when dealing with upgradeable contracts.
 * This library helps with reading and writing to such slots without the need for inline assembly.
 *
 * The functions in this library return Slot structs that contain a `value` member that can be used to read or write.
 *
 * Example usage to set ERC1967 implementation slot:
 * ```
 * contract ERC1967 {
 *     bytes32 internal constant _IMPLEMENTATION_SLOT = 0x360894a13ba1a3210667c828492db98dca3e2076cc3735a920a3ca505d382bbc;
 *
 *     function _getImplementation() internal view returns (address) {
 *         return StorageSlot.getAddressSlot(_IMPLEMENTATION_SLOT).value;
 *     }
 *
 *     function _setImplementation(address newImplementation) internal {
 *         require(Address.isContract(newImplementation), "ERC1967: new implementation is not a contract");
 *         StorageSlot.getAddressSlot(_IMPLEMENTATION_SLOT).value = newImplementation;
 *     }
 * }
 * ```
 *
 * _Available since v4.1 for `address`, `bool`, `bytes32`, and `uint256`._
 */
library StorageSlotUpgradeable {
    struct AddressSlot {
        address value;
    }

    struct BooleanSlot {
        bool value;
    }

    struct Bytes32Slot {
        bytes32 value;
    }

    struct Uint256Slot {
        uint256 value;
    }

    /**
     * @dev Returns an `AddressSlot` with member `value` located at `slot`.
     */
    function getAddressSlot(bytes32 slot) internal pure returns (AddressSlot storage r) {
        assembly {
            r.slot := slot
        }
    }

    /**
     * @dev Returns an `BooleanSlot` with member `value` located at `slot`.
     */
    function getBooleanSlot(bytes32 slot) internal pure returns (BooleanSlot storage r) {
        assembly {
            r.slot := slot
        }
    }

    /**
     * @dev Returns an `Bytes32Slot` with member `value` located at `slot`.
     */
    function getBytes32Slot(bytes32 slot) internal pure returns (Bytes32Slot storage r) {
        assembly {
            r.slot := slot
        }
    }

    /**
     * @dev Returns an `Uint256Slot` with member `value` located at `slot`.
     */
    function getUint256Slot(bytes32 slot) internal pure returns (Uint256Slot storage r) {
        assembly {
            r.slot := slot
        }
    }
}